ext {
    enclaveRepo = "10.131.7.138:7001"
    enclave = InetAddress.getLocalHost().getHostAddress().startsWith("10.131")
}

allprojects {
    group = 'org.sorcersoft.sorcer'
    version = '1.1-SNAPSHOT'
}

apply from: file('gradle/libraries.gradle')

subprojects {
    if (project.name != "examples" && project.name != "sorcer-int-tests") {
        apply plugin: "maven"
        apply plugin: "maven-publish"

        if (project.name != "distribution") {
            apply plugin: "java"
            sourceCompatibility = 1.6
            targetCompatibility = 1.6
            // apply from: file("${rootProject.projectDir.path}/gradle/bootme.gradle")

            // The following parts configurations and sourceSets are required to make the "provided" scope work
            configurations {
                provided
            }
            sourceSets {
                main.compileClasspath += configurations.provided
                test.compileClasspath += configurations.provided
                test.runtimeClasspath += configurations.provided
            }
            // end of "provided" scope

            publishing {
                publications {
                    mavenJava(MavenPublication) {
                        from components.java
                    }
                }
            }
            // Add poms to jars
            jar.dependsOn('generatePomFileForMavenJavaPublication')

            jar {
                into("META-INF/maven/$project.group/$project.name") {
                    from "build/publications/mavenJava/pom-default.xml"
                    rename ".*", "pom.xml"
                }
            }
            // end of add poms to jars

        }

        repositories {
            mavenLocal()

            maven { url "http://mvn.sorcersoft.com/content/groups/public/" }
            maven { url "http://repo.maven.apache.org/maven2" }
            maven { url "http://mvn.sorcersoft.com/content/groups/public" }
        }

    }

}
